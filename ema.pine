//@version=6
strategy("EMA Structure + Recent Cross + Greedy Trail", overlay=true)

// INPUTS 
ema20Len  = input.int(20, title="EMA 20")
ema50Len  = input.int(50, title="EMA 50")
ema100Len = input.int(100, title="EMA 100")
ema200Len = input.int(200, title="EMA 200")

lookback = input.int(10, title="Cross Lookback Bars")
lookfutherback = input.int(20, title="Cross Lookfutherback Bars")

trailStart = input.float(8.0, title="Trail Start %") / 100
trailDist  = input.float(2.0, title="Trail Distance %") / 100

// New thingy: Volume and Stop Loss inputs
volThreshold = input.float(80.0, title="Volume Threshold %") / 100
initialStopPct = input.float(6.0, title="Initial Stop Loss %") / 100

// EMAs 
ema20 = ta.ema(close, ema20Len)
ema50 = ta.ema(close, ema50Len)
ema100 = ta.ema(close, ema100Len)
ema200 = ta.ema(close, ema200Len)

plot(ema20, color=color.orange, linewidth=2, title="EMA 20")
plot(ema50, color=color.blue, linewidth=2, title="EMA 50")
plot(ema100, color=color.green, linewidth=1, title="EMA 100")
plot(ema200, color=color.red, linewidth=1, title="EMA 200")

// STRUCTURE
structureBull =
     ema20 > ema50 and
     ema50 > ema100 and
     ema100 > ema200

// Any recent cross
cross20above50  = ta.barssince(ta.crossover(ema20, ema50))  <= lookback ?1:0
cross20above100 = ta.barssince(ta.crossover(ema20, ema100)) <= lookback ?1:0
cross20above200 = ta.barssince(ta.crossover(ema20, ema200)) <= lookback ?1:0
cross50above20 = ta.barssince(ta.crossover(ema50, ema20)) <= lookfutherback ?1:0

var int totalCross = 0
if cross20above50 == 1 and cross50above20 == 1 
    totalCross := 0
else 
    totalCross := cross20above50 + cross20above100 + cross20above200
crossCondition = totalCross > 0

// New thingy: VOLUME CONDITION 
avgVolume = ta.sma(volume, 10)
volumeCheck = volume > (avgVolume * volThreshold)
rsi = ta.rsi(close, 14) < 50

// Candle Check 
avgClose = ta.sma(close, 10)
openGood = open > avgClose[1]

// EMA Spread
maxEma = math.max(ema20, ema50)
minEma = math.min(ema20,ema50)
emaSpread = ((maxEma - minEma) / minEma) * 100
isEntangled = emaSpread < 1.1

// BUY 
buyCondition = structureBull and crossCondition and volumeCheck and rsi and not isEntangled

if buyCondition
    strategy.entry("BUY", strategy.long)

// STOP LOSS LOGIC 
var float highest = na

if strategy.position_size > 0
    highest := na(highest) ? high : math.max(highest, high)
else
    highest := na

// Check if trailing stop should activate
trailActive = highest >= strategy.position_avg_price * (1 + trailStart)
trailStop  = highest * (1 - trailDist)

// Combined stop logic - use trailing if active, otherwise use initial 6% stop
if strategy.position_size > 0
    stopPrice = trailActive ? trailStop : strategy.position_avg_price * (1 - initialStopPct)
    strategy.exit("EXIT", from_entry="BUY", stop=stopPrice)

// VISUAL
bgcolor(isEntangled ? #ccd613 : na) // colour only for testing. Remove this
bgcolor(structureBull ? color.new(color.green, 95) : na, title="Bull Structure")
plotshape(buyCondition, style=shape.triangleup, location=location.belowbar, 
          color=color.green, size=size.small, title="Buy Signal")

// Plot stop loss level for visualization
plotStopLevel = strategy.position_size > 0 ? (trailActive ? trailStop : strategy.position_avg_price * (1 - initialStopPct)) : na
plot(plotStopLevel, color=color.red, style=plot.style_circles, linewidth=2, title="Active Stop")